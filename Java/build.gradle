apply plugin: 'java'

apply plugin: 'eclipse'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    flatDir { dirs System.properties.'user.home' + '/lib/Java/DataRush/lib' }
}

dependencies {
    compile 'org.codehaus.jcsp:jcsp:1.1-rc5'
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile 'org.codehaus.groovy:groovy-all:2.3.9'
    compile 'org.functionaljava:functionaljava-java8:4.2'
    compile ':datarush-library:5.1.0-49'
    // This is needed by DataRush
    compile 'commons-logging:commons-logging:1.2'
}

sourceCompatibility = 8

defaultTasks 'classes'

new File("${projectDir}/src/main/java/uk/org/winder/pi_quadrature").eachFileMatch(~/Pi_.*\.java/) { filename ->
    final root = filename.name.replaceFirst(/\.java$/, '')
    final className = 'uk.org.winder.pi_quadrature.' + root
    task ('run_' + root, dependsOn: 'classes') << {
        final process = ("java -cp build/classes/main ${className}").execute()
        process.in.eachLine{line -> println line}
        assert process.waitFor() == 0
    }
}

task wrapper (type:Wrapper) {
    gradleVersion = '2.3'
}
