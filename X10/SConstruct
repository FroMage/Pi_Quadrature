# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2008–2013  Russel Winder

import os

packagePath = 'uk/org/winder/pi_quadrature'

x10Environment = Environment(
    ENV=os.environ,
)

outputClass = 'src/{}/Output.x10'.format(packagePath)

compileTargets = []

for item in Glob('src/{}/Pi_*.x10'.format(packagePath)):
    x10ClassName = os.path.splitext(item.name)[0]
    #
    #  Java backend bits.
    #
    x10ClassFile = x10Environment.Command('{}/{}.class'.format(packagePath, x10ClassName), [item, outputClass], 'x10c $SOURCES')
    compileTargets.append(x10ClassFile)
    #
    # C++ backend bits.
    #
    x10Executable = x10Environment.Command(x10ClassName, [item, outputClass], 'x10c++ -O -o $TARGET $SOURCES')
    SideEffect('xxx_main_xxx.cc', x10Executable)
    compileTargets.append(x10Executable)

Default(compileTargets)

Clean('.', 'uk')
