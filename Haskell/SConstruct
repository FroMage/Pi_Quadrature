# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2008–2013 Russel Winder

import os
import sys
sys.path.append('../site_scons')

from executablesupport import compileTargets, addCompileTarget, addRunTarget, createHelp

#  Haskell jobs run in a single thread by default.  Run the executable with "+RTS -Nx" to run with x kernel
#  threads.

haskellEnvironment = Environment(tools=['haskell'])

generatedExtensions = ['.hi', '.o', '.hp', '.ps', '.aux']

compileAllSource = True

if not compileAllSource:
    outputModule = haskellEnvironment.HaskellObject('Output.hs')

for item in Glob('pi_*.hs'):
    root = os.path.splitext(item.name)[0]
    options = ['-rtsopts']
    variant = root.split('_')[1]
    if variant in ('threads', 'parMap', 'parallel'):
        options.append('-threaded')
    if compileAllSource:
        addRunTarget(haskellEnvironment, addCompileTarget(haskellEnvironment.Command(root, [item.name, 'Output.hs'], 'ghc --make -O -o $TARGET' + ''.join([' ' + x for x in options]) + ' $SOURCES')))
        SideEffect([root + ext for ext in generatedExtensions] + ['Output' + ext for ext in generatedExtensions],  root)
    else:
        addRunTarget(haskellEnvironment, addCompileTarget(haskellEnvironment.HaskellProgram([item, outputModule])))

Default(compileTargets)

Help(createHelp())
