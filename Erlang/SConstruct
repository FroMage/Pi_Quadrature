# -*- mode:python; coding:utf-8; -*-

#  Calculation of π using quadrature.
#
#  Copyright © 2008–2013 Russel Winder

import os
import sys
sys.path.append('../site_scons')

from executablesupport import compileTargets, addCompileTarget, addRunTarget, createHelp

erlangEnvironment = Environment(tools=['erlang'])

#  The trailing slash on the OUTPUT is critical for the way the erlang tool works :-(

microsecondTime = addCompileTarget(erlangEnvironment.Erlang('microsecondTime.erl',  OUTPUT='./'))
output = addCompileTarget(erlangEnvironment.Erlang('output.erl',  OUTPUT='./'))

for item in Glob('pi_*.erl'):
    compiledFile = addCompileTarget(erlangEnvironment.Erlang(item))
    runTarget = addRunTarget(erlangEnvironment, compiledFile, '$ERL -noshell -s {} -smp', lambda x: x.replace('.beam', ''))
    Depends(compiledFile, [microsecondTime, output])
    #  Executing an Erlang program can result in a crash dump file so let SCons know this.
    SideEffect('erl_crash.dump',  runTarget[0].name)

Default(compileTargets)

Help(createHelp())
